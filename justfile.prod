set shell := ["bash", "-c"]
export COMPOSE_FILE := "docker-compose.prod.yml"
export FLASK_ENV := "production"


default:
    @just --list

check-env:
    python3 app/load_env.py

up-db:
    docker compose up -d --remove-orphans

up-admin: up-db
    -screen -S vm_manager_admin_prod -X quit
    screen -dmS vm_manager_admin_prod bash -c 'exec gunicorn "app:create_app_admin()" --bind 0.0.0.0:8000 --workers 1'
    @echo "Admin application started"

up-api: up-db
    -screen -S vm_manager_api_prod -X quit
    screen -dmS vm_manager_api_prod bash -c 'exec gunicorn "app:create_app_api()" --bind 0.0.0.0:8080 --workers 4'
    @echo "API application started"


up: up-admin up-api
    @echo "Production application started"

down:
    -pkill -f '[g]unicorn.*app:create_app_admin' || true
    -pkill -f '[g]unicorn.*app:create_app_api' || true
    -screen -S vm_manager_admin_prod -X quit
    -screen -S vm_manager_api_prod -X quit
    docker compose down
    @echo "Production application and containers have been stopped"

logs:
    screen -S vm_manager_api_prod -X hardcopy -h /tmp/vm_manager_api_prod.log
    cat /tmp/vm_manager_api_prod.log

logs-admin:
    screen -S vm_manager_admin_prod -X hardcopy -h /tmp/vm_manager_admin_prod.log
    cat /tmp/vm_manager_admin_prod.log

prune:
    @docker compose down -v

db +args:
    @docker compose up -d --remove-orphans
    flask db {{args}}
    @docker compose down
